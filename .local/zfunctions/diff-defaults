#!/usr/bin/env zsh

zparseopts -D -E -F - \
  {d:,-diff-command:}=diff_command \
  {t:,-timer:}=timer || return 1

if [[ -z $_dbefore ]]; then
  _domain=$1
  if [[ -z $_domain ]]; then
    print "Warning! Domain was not set. All settings will be monitored for changes."
  fi

  _dbefore=`mktemp`
  defaults read $_domain > $_dbefore

  if [[ $? == 0 ]]; then
    print "Ready for changes in ${_domain:-all domains}."
    if [[ -n $timer ]]; then
      print "Diff will be auto applied in ${timer[-1]} seconds."
      sleep ${timer[-1]}
      $0 $diff_command $_domain
    fi
  else
    rm $_dbefore
    unset diff_command timer _domain _dbefore
  fi
else
  if [[ -n $diff_command ]]; then
    _diff=(${(z)diff_command[-1]})
  else
    _diff=(diff -u --color=auto)
  fi

  _dafter=`mktemp`
  defaults read $_domain > $_dafter
  $_diff $_dbefore $_dafter

  rm $_dbefore $_dafter
  unset diff_command timer _diff _domain _dbefore _dafter
fi
