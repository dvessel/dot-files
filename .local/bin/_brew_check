#!/usr/bin/env zsh

# Helper function used to check for a brew installed binaries.
#
# Usage:
#  _brew_check [formulae - optional:]binary [binary]...
#
#  if _brew_check rom-tools:chdman fzf fd; then
#    cmd ...
#    cmd ...
#  fi
#
# Formulae is optional. Use when the binary and formulae names do not match.
# Add as many formulae:binaries as needed. Status code 1 (false) and stderr 
# message on failed checks.
typeset -aU formulae bins
for i in $@; if ! type ${i/*:/} &>/dev/null; then
    formulae+=${i/:*/}; bins+=${i/*:/}
fi
if [[ -n $bins ]]; then
  printf "Required binaries missing: %s. Install with \`brew install %s\`.\n" \
    "${(j:, :)bins}" "$formulae" >&2
  return 1
fi
