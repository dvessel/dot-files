#!/usr/bin/env zsh
set -e

# - Make sure full disk access is enabled for the terminal app so
#   defaults can read from sandboxed apps and their containers.
# - Doesn't account for hosts or -currentHost. I have not run into a any
#   problems without it but there are some settings where it's needed.

# Tested on MacOS 13 Ventura.
# - Uses features in Zsh 5.8, installed with 11 Big Sur.
# - Uses features in plutil available in 10.2 Jaguar.

# $1 exit status
function s.help {
  print -rC1 -- \
  "[backup|restore|list|delete] - Required action." \
  "[-a|--all]                   - Acts on all keys/domains from existing backups." \
  "[-s|--search]                - Search for keys in active settings." \
  "                               Use to list/save keys not previously backed-up." \
  "[-k|--key] [<setting>]       - Settings key to act on." \
  "[-d|--domain] [<domain>]     - Target a domain, e.g., com.apple.finder." \
  "[-g|-globalDomain]           - Global domain. Conflicts with -d." \
  "[-p|--path] [<path>]         - Defaults to ~/.config/backup/defaults." \
  "[-h|--help]                  - This help text."
  exit $1
}

# $1 domain
# $2 key
# $3 output 1 = domain or 2 = key
function op.backup {
  if ! s.sourceDomainExists $1; then
    echo "? $1 - `s.error source domain does not exist!`" >&2
    return 1
  fi

  local type value index plist=$bdpath/$1.plist

  # Keys from `defaults read-type` translated to values accepted by plutil.
  # Data, date, arrays and dictionaries must be added as xml.
  typeset -A types=(
    "Type is boolean"     -bool
    "Type is integer"     -integer
    "Type is float"       -float
    "Type is string"      -string
    "Type is data"        -xml
    "Type is date"        -xml
    "Type is array"       -xml
    "Type is dictionary"  -xml
  )

  if type=$types[`defaults read-type $1 $2 2>/dev/null`]; then
    if [[ ! -f $plist ]]; then
      plutil -create xml1 $plist
    fi
    # Hold keys to list later. No other way to do it AFAIK.
    if ! plutil -type SettingsIndex $plist &>/dev/null; then
      plutil -insert SettingsIndex -array $plist
    fi

    case $type in
      -bool)            typeset -A bool=( 0 false 1 true )
                        value=$bool[`defaults read $1 $2`]  ;;
      -integer|-float)  value=`defaults read $1 $2`         ;;
      -xml)             value="`defaults export $1 - |
                        plutil -extract "${2//\./\\.}" xml1 -o - -- -`" ;;
      *)                value="`defaults read $1 $2`"       ;;
    esac

    # Escape dots or plutil will drill into submembers.
    if plutil -replace "${2//\./\\.}" $type $value $plist; then
      echo "  $@[$3] - `s.ok saved`"
      if ! s.getSettingsIndex $1 | grep -qx $2; then
        plutil -insert SettingsIndex -string $2 -append $plist
      fi
    fi
  else
    echo "? $@[$3] - `s.error source key does not exist!`" >&2
    return 1
  fi
}

# $1 domain
# $2 key
# $3 output 1 = domain or 2 = key
function op.restore {
  if ! s.sourceDomainExists $1; then
    echo "? $1 - `s.error restore target does not exist!`" >&2
    return 1
  fi

  local value

  if value=`plutil -extract "${2//\./\\.}" xml1 -o - $bdpath/$1.plist 2>/dev/null`; then
    defaults write $1 $2 "$value" &&
      echo "  $@[$3] - `s.ok restored`"
  else
    echo "!!$@[$3] - `s.error failed reading backup!`" >&2
    return 1
  fi
}

# $1 domain
# $2 key
# $3 output 1 = domain or 2 = key
function op.delete {
  if l=`s.getSettingsIndex $1 | grep -nx $2`; then
    plutil -remove SettingsIndex.$((${l/:*} - 1)) $bdpath/$1.plist
    plutil -remove "${2//\./\\.}" $bdpath/$1.plist &&
      echo "  $@[$3] - `s.ok deleted`"
  else
    echo "!!$@[$3] - `s.error backup key does not exist!`" >&2
    return 1
  fi
}

# $1 domain
# $2 key
function op.list {
  echo "  $2"
}

# $1 domain
# $2 key
function op.listDomain {
  echo "  $1"
}

# $1 domain
# $2 key
function s.backupKeyExists {
  s.getSettingsIndex $1 | grep -qx $2
}

# $1 domain
function s.sourceDomainExists {
  typeset -g defaultsDomains
  if [[ -z $defaultsDomains ]]; then
    defaultsDomains="NSGlobalDomain\n`defaults domains | sed 's/, /\n/g'`"
  fi
  echo $defaultsDomains | grep -qix $1
}

# $1 domain
function s.getSettingsIndex {
  c=`plutil -extract SettingsIndex raw $bdpath/$1.plist`
  if [[ $c > 0 ]]; then
    for n in {0..$((c-1))}; plutil -extract SettingsIndex.$n raw $bdpath/$1.plist
  fi
}

function s.getBackupDomains {
  for d in $bdpath/*.plist(N); echo $d:r:t
}

function s.header {
  # Uppercase first character.
  echo "\e[1;30m${(C)1::1}${1:1}\e[0m"
}
function s.error {
  printf "\e[0;31m%s\e[0m" "${(j[ ])@}"
}
function s.ok {
  printf "\e[0;32m%s\e[0m" "${(j[ ])@}"
}
function s.warn {
  printf "\e[0;33m%s\e[0m" "${(j[ ])@}"
}

function {
  local op key domain search all help errmsg
  typeset -g bdpath

  # Zsh 5.8 required for -F option (exit on invalid flag).
  zmodload zsh/zutil
  zparseopts -D -E -F - \
    {a,-all}=all \
    {s,-search}=search \
    {k:,-key:}=key \
    {g,globalDomain,d:,-domain:}=domain \
    {p,-path}:=bdpath \
    {h,-help}=help || s.help 1

  if [[ ${#@[@]} > 0 ]]; then
    op=$1; shift 1
  fi

  [[ -n $help ]] && s.help

  [[ $op != (backup|restore|list|delete) ]] && errmsg+=(
    "backup, restore, list or delete action is required."
  )
  [[ $op == (backup|restore|delete) && ! -n $key$domain$all ]] && errmsg+=(
    "$op requires a key, domain or the -a flag."
  )
  [[ -n $search && -z $key ]] && errmsg+=(
    "Search requires a key."
  )
  [[ -n $search && $op == (restore|delete) ]] && errmsg+=(
    "Cannot $op with search! Omit the $search flag."
  )
  [[ $op == list && -n $domain && -n $key ]] && errmsg+=(
    "Cannot list with $domain[1] and $key[1] !"
  )
  [[ ${#domain[@]} > 2 ]] && errmsg+=(
    "Conflicting domains: $domain"
  )
  [[ ${#@[@]} > 0 ]] && errmsg+=(
    "Unknown arguments: $@"
  )

  bdpath=${bdpath[-1]:-~/.config/backup/defaults}
  if [[ ! -d $bdpath ]]; then
    case $op in
      backup)       mkdir -p $bdpath                            ;;
      restore|list) errmsg+=("No backups found in path: $bdpath");;
    esac
  fi

  if [[ -n $errmsg ]]; then
    printf " %s\n" $errmsg >&2
    s.help 1
  fi

  domain=${domain[-1]/-(globalDomain|g)/NSGlobalDomain}
  # Domains are case sensitive. Check when possible. Ignore otherwise.
  if test ! -z $domain && s.sourceDomainExists $domain; then
    if [[ $domain != `echo $defaultsDomains | grep -ix $domain` ]]; then
      domain=`echo $defaultsDomains | grep -ix $domain`
      echo "`s.warn Domain is case sensitive. Using $domain.`"
    fi
  fi

  if [[ -n $search ]]; then

    s.header "Searching active domains for $key:"
    # [backup|list] from search.
    op=${op/list/listDomain}
    while read -r l; do
      if [[ $l =~ "^Found [0-9]+ keys in domain '([^']*)': {$" ]]; then
        _mdomain=${match[1]//Apple Global Domain/NSGlobalDomain}
        # domain filtering. Is case insensitive.
        if [[ -z $domain || $_mdomain:l == $domain:l ]]; then
          m=m
        fi
      # If previously matched a domain, verify it's for the right key.
      elif [[ -n $m && $l =~ "^\"?$key[-1]\"? =" ]]; then
        op.$op $_mdomain $key[-1] 1
        unset m
      fi
    done < <( defaults find $key[-1] )

  else

    # list all domains for a given key.
    if [[ $op == list && -n $key ]]; then
      s.header "$op domains for $key:"
      while read -r d; do
        ! s.backupKeyExists $d $key[-1] ||
          op.listDomain $d
      done < <( s.getBackupDomains )

    # [backup|restore|delete] a specific key for specific domain.
    elif [[ -n $domain && -n $key ]]; then
      s.header "$op $key[-1] for $domain:"
      op.$op $domain $key[-1] 2

    else
    # [backup|restore|list|delete] from saved keys. One filter allowed.
    # - filter by domain - applies to all keys within a domain.
    # - filter by key    - applies to a key across all domains.
      while read -r d; do
        while read -r k; do
          if [[ -z $domain || $domain == $d ]] &&
             [[ -z $key || $key[-1] == $k ]];
          then
            if [[ -z $h ]] && h=h; then
              s.header "$op ${key[-1]:-keys} for $d:"
            fi
            op.$op $d $k 2
          fi
        done < <( s.getSettingsIndex $d )
        unset h
      done < <( [[ -n $domain ]] && echo $domain || s.getBackupDomains )

    fi
  fi
} $@
